PROC GLOBAL
{ DHS-V secondary editing application  Version 1.0.0 of 09/22/08 }

{+MEN}
  { To store couples information to compare at postproc level 1            }
  { Columns are: 1-Resp. line  2-Resp. marital status  3-husband/wife line }
  array womhusb(20,3);
  array manwife(20,3);
{MEN+}

  { Arrays to check barcodes for HIV and Malaria }
  array transHIV(200);                   { array to know barcodes in the transmittal that are not in questionnaires for HIV }
  array transMAL(200);                   { array to know barcodes in the transmittal that are not in questionnaires for MALARIA }

  string transname;                      { transmittal sheet file names }
  array string barcodeHIV(200);        { stores barcodes for HIV }
  array string barcodeMAL(200);        { stores barcodes for MALARIA or other biomarker }

 { --- working variables ------------------------------------------------------------------------ }

  numeric b, e, i, j, k, m, mRow, n, t, v, x, y;
  numeric xho, xhc, xhi; { # HHs, # HHs completed, # HHs incomplete }
  numeric ldt, udt, cmcTerm, tryMatch;
  numeric xrelat, xsex, xlives, xslept, xage, xmother, xfather, xelig, xeligc, xeligm, xusing;
  numeric run1, doimp, dprev, ver100, ignoreY, healthy, emsample;
  numeric yage, hwage, err, rok, fline_valid, mLine_valid, hwlcmc, hwucmc, ceb;
  numeric goodLink, found, xdeath, discont, condom;
  numeric swrt, anew, aold, iold, aoldest, ETsuppress;
  numeric prevCL, firstcase; { previous cluster # }
  numeric temp, di, minai, maxai, minaim, maxaim, head, valf, valm, rc, rp, ipos;
  numeric maxelig, maxevent, maxmemb, LastSrc, MinAsx;
  numeric tbcodesHIV, tbcodesMal;

PROC CGHH80_FF
preproc
  { get language from calling menu }
  SetLanguage( GetLanguage() );

  { set application parameters }
  minai    = 15;        { !!! Minimum age at interview - women         }
  maxai    = 59;        { !!! Maximum age at interview - women         }
  {+MEN}
  minaim   = 15;        { !!! Minimum age at interview - men           }
  maxaim   = 59;        { !!! Maximum age at interview - men           }
  {MEN+}
  maxelig  = 20;        { !!! Maximum number of eligible men or women  }

  prevCL   = notappl;           { previous cluster number }
  dprev    = 1;         // !!! 1-review by DP specialist
  doimp    = 0;         // !!! 1-when running imputation

  firstcase = 1;

{ --------------------------------------------------------------------------- }
postproc 	{ of PROC CORE8_FF }

  { check that barcodes in transmittal sheet for the cluster were matched/found in the QREs }
  do i = 1 while i <= tbcodesHIV
    if !transHIV(i) then
      errmsg( 0172, TCLUSTER, barcodeHIV(i), tr("HIV") );
    endif;
  enddo;
  do i = 1 while i <= tbcodesMAL
    if !transMAL(i) then
      errmsg( 0172, TCLUSTER, barcodeMAL(i), tr("Malaria") );
    endif;
  enddo;


{ --------------------------------------------------------------------------- }
PROC HOUSEHOLD
preproc

  di = cmcode( QHINTM, QHINTY );

  if YCLUSTER <> QHCLUST then 
    YCLUSTER = QHCLUST; 
    clear(CLUSTERS); 
    if loadcase(CLUSTERS, YCLUSTER) then 
      if YREGION <> QHREGION then  
        errmsg( 0015, QHREGION,QHREGION,YREGION,YREGIONN ) 
      endif; 
      if YURBRUR <> QHTYPE then  
        errmsg( 0017, QHTYPE,QHTYPE,YURBRUR,GetLabel(QHTYPE,YURBRUR) ); 
      endif; 
    else 
      errmsg( 0019, YCLUSTER); 
      YCLUSTER = notappl; 
    endif; 
  endif; 

  { !!! HIV and Malaria }
  if firstcase then
    { initialize array to check barcodes between transmittal sheet and questionnaires }
    do i = 1 while i <= 200
      transHIV(i) = 0;
      transMAL(i) = 0;
    enddo;
    { !!! HIV }
    { load transmittal sheets to make sure that all barcodes for HIV in questionnaires are defined in transmittal sheets and vice versa }
    TCLUSTER = QHCLUST;
    tbcodesHIV = 0;
    if !loadcase( TRANSMIT, TCLUSTER )  then
      errmsg( 0170, tr("HIV"), TCLUSTER );
      clear( TRANSMIT );
    else
      tbcodesHIV = TBCODES;
    endif;
    do i = 1 while i <= tbcodeshiv
      barcodeHIV(i) = TBARCODE(i);
    enddo;

    firstcase = 0;
  endif;
  { !!! HIV and Malaria }

  {+MEN}
  { Initialize the working woman and men's line number to be used to check  }
  { the marital status of couples at the end of the questionnaire           }
  do i = 1 while i <= maxelig
    do j = 1 while j <= 3
      womhusb(i,j) = 0;
      manwife(i,j) = 0;
    enddo;
  enddo;
  {MEN+}

  { *** enable at imputation time
  { Check totals from previous cluster -- when running with multiple clusters }
  if CSAMPLE <> QHCLUST & CSAMPLE <> 0 then
    open( CONTROL );
    if loadcase( CONTROL, CSAMPLE ) then
      if  xho = chtotal
        & xhc = chcomp
        & xhi = chincomp
        & xww = cwtotal
        & xwc = cwcomp
        & xwi = cwincomp
{+MEN}
        & xmm = cmtotal
        & xmc = cmcomp
        & xmi = cmincomp
{MEN+}
      then
        do x = 1 while x <= 3 & CEDIT(x)
          { find the first empty entry in CEDIT }
        enddo;
        CEDIT(x) = SYSDATE();
        if !writecase( CONTROL, CSAMPLE ) then
          errmsg( 0007, CSAMPLE )
        endif
      else
        errmsg( 0008, CSAMPLE,
                xho,    xhc,   xhi,     xww,    xwc,   xwi,     xmm,    xmc,   xmi,
                chtotal,chcomp,chincomp,cwtotal,cwcomp,cwincomp,cmtotal,cmcomp,cmincomp )
      endif
    endif;
    close( CONTROL );
  endif;

  { Check this cluster is ready for checking }
  if QHCLUST <> CSAMPLE then
    CSAMPLE = QHCLUST;
    open( CONTROL );
    if loadcase( CONTROL, CSAMPLE ) then
      if !CVERIF then
        errmsg( 0006, CSAMPLE );
        stop
      { !!! HIV }
      elseif !CHIVDATE then
        errmsg( 0171, QHCLUST );
        stop
      { !!! HIV }
      else
        run1 = (CEDIT(1) = 0);
        xho = 0;              { Counts of questionnaires }
        xhc = 0;
        xhi = 0;
        xww = 0;
        xwc = 0;
        xwi = 0;
      {+MEN}
        xmm = 0;
        xmc = 0;
        xmi = 0;
      {MEN+}
      endif;
    else
      errmsg( 0005, CSAMPLE );
      stop
    endif;
    close( CONTROL );
  endif;

  { it is important to load weights in the preproc because they are needed by individual questionnaires }
  if doimp then
  { !!!! uncomment at imputation time }
    QHINTC = cmcode( QHINTM, QHINTY );
    QHINTCD = CDCode( QHINTY, QHINTM, QHINTD );
    { Following code to add weights to the data set }
    if QHCLUST <> prevCL then
      loadcase( WEIGHTS, QHCLUST ); { !!! adjust this file as necessary }
    endif;
    if QHRESULT = 1 then
      QHWEIGHT = HHWEIGHT;         { !!! Household weight (total) in weights dictionary }
      if QHELIGM = 1 then
        QMWEIGHT = HHMWEIGHT;      { !!! Household weight for men in weights dictionary }
      else
        QMWEIGHT = 0;
      endif;
    else
      QHWEIGHT = 0;
      QMWEIGHT = 0;
    endif;
    prevCL = QHCLUST;   { update here }
  endif;
  *** }

  { Set run1 on if doing DP review, but not yet ready for imputation }
  if dprev & !doimp then
    run1 = 1;
  endif;

  xho   = xho + 1;
  if QHRESULT = 1 then
    xhc = xhc + 1
  else
    xhi = xhi + 1
  endif;

  { Verify that interviewer is registered in fieldworker questionnaire }
  if !loadcase( FIELDWKQ, QHINTNUM ) then
    errmsg( 0025, tr("Interviewer"), QHINTNUM );
  endif;
  { Verify that supervisor is registered in fieldworker questionnaire }
  if !loadcase( FIELDWKQ, QHSUPERV ) then
    errmsg( 0025, tr("Supervisor"), QHSUPERV );
  endif;

postproc
  { ***** enable these instructions at imputation time when joining HHEdit and INEdit 
  {+MEN}
  { check what woman declared about her husband }
  do i = 1 while i <= maxelig & womhusb(i,1) <> 0
    if womhusb(i,3) then
      found = 0;
      goodlink = 0;
      do j = 1 while j <= maxelig & manwife(j,1) <> 0
        if womhusb(i,3) = manwife(j,1) then
          found = j;
          if womhusb(i,1) = manwife(j,3) then
            goodlink = j;
            if !manwife(j,2) in 1,2 then
              errmsg( 99990, womhusb(i,1), womhusb(i,3), manwife(j,2) )
            endif;
          endif;
        endif;
      enddo;
      if found & !goodlink then
        errmsg( 99995, womhusb(i,1), womhusb(i,3), manwife(found,3) )
      endif;
    endif;
  enddo;

  { check what man declared about his wife }
  do i = 1 while i <= maxelig & manwife(i,1) <> 0
    if manwife(i,3) then
      do j = 1 while j <= maxelig & womhusb(j,1) <> 0
        if manwife(i,3) = womhusb(j,1) then
          if !womhusb(j,2) in 1,2 then
            errmsg( 99990, manwife(i,1), manwife(i,3), womhusb(j,2) )
          endif;
          if manwife(i,1) <> womhusb(j,3) then
            errmsg( 99995, manwife(i,1), manwife(i,3), womhusb(j,3) )
          endif;
          break;
        endif;
      enddo
    endif;
  enddo;

  { Check for two men reporting the same wife }
  do i = 2 while i <= maxelig & manwife(i,1) <> 0
    if manwife(i,3) <> 0 then
      do j = 1 while j < i
        if manwife(i,3) = manwife(j,3) then
          errmsg( 99997, manwife(j,1), manwife(i,1), manwife(j,3) );
        endif;
      enddo;
    endif;
  enddo;
  {MEN+}
  **** end of instructions for imputation }

{ --------------------------------------------------------------------------- }
PROC QHSEC01_EDT

  { Only one head allowed }
  temp = count( $ where QH03 = 1 );
  if temp <> 1 then
    errmsg( 0029, temp )
  endif;

  { Only one spouse, parent, parent-in-law allowed }
  if count( $ where QH03 = 2 {+POLYG} & QH04 = 1 {POLYG+} ) >= 2  { !!! uncomment if polygyny in country }
   | count( $ where QH03 = 6 & QH04 = 1 ) >= 2
   | count( $ where QH03 = 6 & QH04 = 2 ) >= 2
   | count( $ where QH03 = 7 & QH04 = 1 ) >= 2
   | count( $ where QH03 = 7 & QH04 = 2 ) >= 2 then
      if run1 & !dprev then errmsg( 0030 ) endif;
  endif;

  for i in $ do

    xrelat  = QH03;
    xsex    = QH04;
    xlives  = QH05;
    xslept  = QH06;
    xage    = QH07;
    xmother = QH13;
    xfather = QH15;
    xelig   = QH09;
    xeligc  = QH11;
    {+MEN}
    xeligm  = QH10;
    {MEN+}

    { All members must be either usual resident or }
    { have slept previous night in household       }
    if xlives <> 1 & xslept <> 1 then
      errmsg( 0050, i, xlives, xslept );
    endif;

    { level/year school completed vs current school year }
    { duplicate when level/year school previous vs current school year asked !!}
    if !special(QH17A) & !special(QH19A) &
       ( QH17A > QH19A | ( QH17A = QH19A & valid(QH17B) & QH17B > QH19B ) ) then
      errmsg( 0160, i, "QH17", QH17A, QH17B, "QH19", QH19A, QH19B );
    endif;

    { Invalid father's line number }
    if xfather <> missing & ( xfather > QHMEMBER | xfather = i ) then
      errmsg( 0120, i, xfather, tr("father") );
    endif;
    { Invalid mother's line number }
    if xmother <> missing & ( xmother > QHMEMBER | xmother = i ) then
      errmsg( 0120, i, xmother, tr("mother") );
    endif;

    { Check eligibility }
    { Eligible woman }
    if xsex = 2 & xage in minai:maxai then
      if xelig <> i then
        errmsg( 0150, i, xsex, xage, "QH09", xelig );
      endif
    else
      if xelig <> 0 then
        errmsg( 0150, i, xsex, xage, "QH09", xelig );
      endif
    endif;
    {+MEN}
    { Eligible man }
    if xsex = 1 & xage in minaim:maxaim & QHELIGM = 1 then
      if xeligm <> i then
        errmsg( 0150, i, xsex, xage, "QH10", xeligm );
      endif
    else
      if xeligm <> 0 then
        errmsg( 0150, i, xsex, xage, "QH10", xeligm );
      endif
    endif;
    {MEN+}
    { Eligible child }
    if xage in 0:5 then
      if xeligc <> i then
        errmsg( 0150, i, xsex, xage, "QH11", xeligc );
      endif
    else
      if xeligc <> 0 then
        errmsg( 0150, i, xsex, xage, "QH11", xeligc );
      endif
    endif;

{ --------------------------------------------------------------------------- }

    { Check relationships }
    { ------------------- }

    { Check age of head of household or spouse is at least 12 }
    if xrelat in 1,2 & xage in 0:11 then
      if run1 then errmsg( 0071, i, xage, 12 ) endif;
    endif;

    { Check age of household head, 12 years younger than parents }
    if xrelat in 6,7 & xage <> missing then
      do j = 1 while j <= QHMEMBER
        if QH03(j) = 1 then
          yage = QH07(j);
          if yage in 0:97 & yage+15 > xage then
            if run1 then errmsg( 0072, j, yage, i, xage ) endif;
          endif;
          break;
        endif;
      enddo;
    endif;

    { Check age of spouse, 12 years younger than parents }
    if xrelat in 6,7 & xage <> missing then
      do j = 1 while j <= QHMEMBER
        if QH03(j) = 2 then
          yage = QH07(j);
          if yage in 0:97 & yage+15 > xage then
            if run1 then errmsg( 0073, j, yage, i, xage ) endif;
          endif;
          break;
        endif;
      enddo;
    endif;

    { Check spouse is opposite sex from Head of Household }
    if xrelat = 2 then
      do j = 1 while j <= QHMEMBER
        if QH03(j) = 1 then
          if QH04(j) = xsex then
            errmsg( 0060, i, xsex, i, QH04(j), j );
          endif;
          break;
        endif;
      enddo;
    endif;

    { Check age of s/d-in-law, 15 years or higher }
    if xrelat = 4 & xage <> missing & xage < 15 then
      if run1 then errmsg( 0075, i, xage ) endif;
    endif;

    { s/d-in-law and head and valid marital status }
    if xrelat = 4 & !QH08(i) in 1:4 then
      if run1 then errmsg( 0076, i, QH08(i) ) endif;
    endif;

    { Find head of household }
    do head = 1 while head <= noccurs($) & QH03(head) <> 1 enddo;
    if head > noccurs($) then
      head = default;
    endif;
    { if member is son or daughter of head, check head's line given as parent }
    if xrelat = 3 & xage in 0:17 & head <> default then
      if     QH04(head) = 1 & xfather <> head then
        errmsg( 0061, i, xrelat, head, tr("father"), "QH15", xfather )
      elseif QH04(head) = 2 & xmother <> head then
        errmsg( 0061, i, xrelat, head, tr("mother"), "QH13", xmother )
      endif;
    endif;

{ --------------------------------------------------------------------------- }

    { Check mother's line }
    { ------------------- }
    mline_valid = 0;               { mother alive and valid relationship }
    if xmother in 1:QHMEMBER then
      { Mother female and at least 12 years older }
      if QH04(xmother) <> 2 then
        errmsg( 0121, i, tr("mother"), xmother )
      elseif QH07(xmother) < xage+12 then
        if run1 then errmsg( 0124, i, tr("mother"), xmother, QH07(xmother) ) endif;
      else
        { consistency between child's mother relationship to head QH03(xmother) }
        { and child relationship to head (xrelat)                               }
        if !valrelat( QH03(xmother), xrelat ) then   { Relationship not OK }
          errmsg( 0122, i, xrelat, tr("mother"), xmother, QH03(xmother) )
        else
          mline_valid = 1;
        endif
      endif;
    endif;

    { Son-Daughter/Head/Spouse/grandchild but no valid mother's line }
    if xmother <> notappl & mline_valid = 0 & xrelat in 1:3,5 then
      { father relationship to child }
      valf = 1;                                               { for relationship other than son/son-in-law }
      if xfather in 1:QHMEMBER & QH03(xfather) in 3,4 then valf = QH03(xfather) endif; { son or son-in-law }
      rc = xrelat;
      do j = 1 while j <= QHMEMBER
        rp = QH03(j);
        recode rc :: rp  :: valf -> rok; { CHILD        - MOTHER       - FATHER        }
                1 :: 6   ::      -> 1;   { Head         - Parent                       }
                2 :: 7   ::      -> 1;   { Spouse       - Parent-in-law                }
                3 :: 1,2 ::      -> 1;   { Son/Daughter - Head/Spouse                  }
                5 :: 3,4 ::   1  -> 1;   { Grandchild   - Daughter(-in-law)            }
                5 ::  3  ::   4  -> 1;   { Grandchild   - Daughter     - son-in-law    }
                5 ::  4  ::   3  -> 1;   { Grandchild   - In-Law       - son           }
                  ::     ::      -> 0;
        endrecode;
        if QH04(j) = 2 & rok then   { Found father }
          if xmother <> j then      { but father not same as father's line }
            errmsg( 0123, i, tr("mother"), "QH13", xmother, j )
          endif;
          break;
        endif;
      enddo;
    endif;

{ --------------------------------------------------------------------------- }

    { Check father's line }
    { ------------------- }
    fline_valid = 0;               { father alive and valid relationship }
    if xfather in 1:QHMEMBER then
      { Father male and at least 15 years older }
      if QH04(xfather) <> 1 then
        errmsg( 0121, i, tr("father"), xfather )
      elseif QH07(xfather) < xage+15 then
        if run1 then errmsg( 0124, i, tr("father"), xfather, QH07(xfather) ) endif;
      else
        { consistency between child's father relationship to head QH03(xfather) }
        { and child relationship to head (xrelat)                               }
        if !valrelat( QH03(xfather), xrelat ) then   { Relationship not OK }
          errmsg( 0122, i, xrelat, tr("father"), xfather, QH03(xfather) )
        else
          fline_valid = 1;
        endif
      endif;
    endif;

    { Son/Daughter/Head/Spouse but father's line not father }
    if xfather <> notappl & fline_valid = 0 & xrelat in 1:3,5 then
      { mother relationship to child }
      valm = 1;                                               { for relationship other than son/son-in-law }
      if xmother in 1:QHMEMBER & QH03(xmother) in 3,4 then valm = QH03(xmother) endif; { son or son-in-law }
      rc = xrelat;
      do j = 1 while j <= QHMEMBER
        rp = QH03(j);
        recode rc :: rp  :: valm -> rok; { CHILD        - FATHER       - MOTHER        }
                1 :: 6   ::      -> 1;   { Head         - Parent                       }
                2 :: 7   ::      -> 1;   { Spouse       - Parent-in-law                }
                3 :: 1,2 ::      -> 1;   { Son/Daughter - Head/Spouse                  }
                5 :: 3,4 ::   1  -> 1;   { Grandchild   - Son(-in-law)                 }
                5 ::  3  ::   4  -> 1;   { Grandchild   - Son          - daughter-in-law }
                5 ::  4  ::   3  -> 1;   { Grandchild   - In-Law       - daughter      }
                  ::     ::      -> 0;
        endrecode;
        if QH04(j) = 1 & rok then   { Found mother }
          if xfather <> j then      { but mother not same as mother's line }
            errmsg( 0123, i, tr("father"), "QH15", xfather, j )
          endif;
          break;
        endif;
      enddo;
    endif;

  enddo;

{ -------------------------------------------------------------------------- }
{ number of eligible women and men with cover page }
PROC QHSEC02_EDT
  x = count( QHSEC01_EDT where QH04 = 2 & QH07 in minai:maxai {& QH08 = ever married });   {!!!}
  if x <> QHWOMEN then
    errmsg( 0151, tr("women"), QHWOMEN, x )
  endif;
  {+MEN}
  x = count(QHSEC01_EDT where QH04 = 1 & QHELIGM = 1 & QH07 in minaim:maxaim );            {!!!}
  if x <> QHMEN then
    errmsg( 0151, tr("men"), QHMEN, x )
  endif;
  {MEN+}

  { consistency on electricity }
  if QH132A <> 1 & QH117 = 1 then
    errmsg( 0165, QH132A, tr("cooking"), "QH117", QH117 )
  endif;
  if QH132A <> 1 & QH125 = 1 then
    errmsg( 0165, QH132A, tr("heating"), "QH125", QH125 )
  endif;
  if QH132A <> 1 & QH126 = 1 then
    errmsg( 0165, QH132A, tr("lighting"), "QH126", QH126 )
  endif;

{ --------------------------------------------------------------------------- }
{ biomarker questionnaire }
PROC QBSECOVER_EDT
  if doimp then
    QBINTC  = cmcode( QBINTM, QBINTY );
    QBINTCD = CDCode( QBINTY, QBINTM, QBINTD );
  endif;

{ --------------------------------------------------------------------------- }
{ biomarkers for children }
PROC QBSEC01_EDT

  for i in $ do
    if validyr(QB103Y) then
      hwlcmc = setlb( QB103M, QB103Y, 0 );
      hwucmc = setub( QB103M, QB103Y, 9999 );
    else
      hwlcmc = di-71;
      hwucmc = di;
    endif;
    hwage = di - hwlcmc;

    if !QB106 in 99.90:99.98,missing then
      if CheckWeight( QH04(QB102), hwage, QB106 ) & run1 then
        errmsg( 0440, tr("Child"), i, QB102, "QB106", QB106, hwage, QH04(QB102) )
      endif;
      { !!! If using SECA 874 scale check if 2nd decimal is in 0,5 }
      { !!! If using SECA 878 scale check if 2nd decimal is equal to 0}
      { !!! If using UNICEF scale, there should be no 2nd decimal at all and the weight field should have only one decimal – but also check if 2nd decimal is 0 }
      if !GetDecimal( QB106, 2 ) in 0,5 then
        errmsg( 441, i, QB102, QB106 );
      endif;
    endif;

    if !QB108 in 999.0:999.8,missing then
      if CheckHeight( QH04(QB102), hwage, QB108 ) & run1 then
        errmsg( 0450, tr("Child"), i, QB102, "QB108", QB108, hwage, QH04(QB102) )
      endif
    endif;

    { Measured lying up to 23 months, standing from 24 months }
    if (di - hwlcmc) < 24 & QB110 = 2 |
       (di - hwucmc) > 23 & QB110 = 1 then
      if !ver100 then errmsg( 0460, i, QB102, QB110, di-hwlcmc ) endif;
    endif;

    { check implausible hemoglobin levels }
    if QB123 in 0.0:1.99,25.6:98.0 then
      errmsg( 0473, tr("Child"), i, QB102, QB123 );
    endif;
    { Verify that biomarker is registered in fieldworker questionnaire }
    if !loadcase( FIELDWKQ, QB115  ) then
      errmsg( 0026, QB115, tr("anthropometry"), tr("measurer"), tr("Child"), i );
    endif;
    if !loadcase( FIELDWKQ, QB116 ) then
      errmsg( 0026, QB116, tr("anthropometry"), tr("assistant"), tr("Child"), i );
    endif;
    if !loadcase( FIELDWKQ, QB121  ) then
      errmsg( 0026, QB121, tr("hemoglobin"), tr("measurer"), tr("Child"), i );
    endif;

  enddo;
  { !!!! if malaria or other biomarker collected for children, remove this comment 
  { check if barcodes recorded in children's record are in the transmittal sheet }
  for i in record QBSEC01 do
    if !AHBARMAL in "?","99990":"99998" & length( strip(AHBARMAL) ) then
      do j = 1 while j <= tbcodesMAL
        if AHBARMAL = barcodeMAL(j) then
          { check for duplicates }
          if transMAL(j) then
            errmsg( 0173, QHCLUST, QHNUMBER, barcodeMAL(k), tr("Child"), tr("Malaria"), i );
          { otherwise mark the barcode as found with the household number }
          else
            transMAL(j) = QHNUMBER
          endif;
          break;
        endif;
      enddo;
      if j > tbcodesMAL then
        { barcode in questionnaire wasn't found in transmittal sheet }
        errmsg( 0174, QHCLUST, QHNUMBER, AHBARMAL, tr("Child"), tr("Malaria"), i );
      endif;
    endif;
  enddo;
  **** end comment for malaria for children }

{ --------------------------------------------------------------------------- }
{ hgt/wgt for women }
PROC QBSEC02_EDT

  for i in $ do
    hwage = QH07(QB202);

    { weight for women }
    if run1 & !QB205 in 20.0:150.0,999.94:999.96,missing then
      errmsg( 0440, tr("woman"), i, QB202, "QB205", QB205, hwage, 2 )
    endif;

    { height for women }
    if run1 & !QB207 in 100.0:200.0,999.4:999.6,missing then
      errmsg( 0450, tr("woman"), i, QB202, "QB207", QB207, hwage, 2 )
    endif;

    { Verify that biomarker is registered in fieldworker questionnaire }
    if QB209 <> notappl & !loadcase( FIELDWKQ, QB209 ) then
      errmsg( 0026, QB209, tr("anthropometry"), tr("measurer"), tr("woman"), i );
    endif;
    if QB210 <> notappl & !loadcase( FIELDWKQ, QB210 ) then
      errmsg( 0026, QB210, tr("anthropometry"), tr("assistant"), tr("woman"), i );
    endif;
    if QB223 <> notappl & !loadcase( FIELDWKQ, QB223 ) then
      errmsg( 0026, QB223, tr("other test"), tr("measurer"), tr("woman"), i );
    endif;
    if QB227 <> notappl & !loadcase( FIELDWKQ, QB227 ) then
      errmsg( 0026, QB227, tr("other test"), tr("measurer"), tr("woman"), i );
    endif;
    if QB215 <> notappl & !loadcase( FIELDWKQ, QB215 ) then
      errmsg( 0026, QB215, tr("other test"), tr("measurer"), tr("woman"), i );
    endif;

    { !!! HIV - other test }
    { check for duplicate barcodes among HH questionnaires within a cluster }
    if length( strip(QB240) ) & !QB240 in "99992":"99996", "?" then
      do k = 1 while k <= tbcodesHIV
        if QB240 = barcodeHIV(k) then
          if transHIV(k) then
            errmsg( 0173, QHCLUST, QHNUMBER, barcodeHIV(k), tr("woman"), tr("HIV"), i );
          endif;
          transHIV(k) = transHIV(k) + 1;
          break;
        endif;
      enddo;
      if k > tbcodesHIV then
        errmsg( 0174, QHCLUST, QHNUMBER, QB240, tr("woman"), tr("HIV"), i );
      endif;
    endif;
    { !!! HIV }
  enddo;

{ --------------------------------------------------------------------------- }
{ hgt/wgt for men }
PROC QBSEC03_EDT

  for i in $ do
    hwage = QH07(QB302);

    { weight for men }
    if run1 & !QB305 in 20.0:150.0,999.94:999.96,missing then
      errmsg( 0440, tr("man"), i, QB302, "QB305", QB305, hwage, 1 )
    endif;

    { height for men }
    if run1 & !QB307 in 100.0:200.0,999.4:999.6,missing then
      errmsg( 0450, tr("man"), i, QB302, "QB307", QB307, hwage, 1 )
    endif;

    { Verify that biomarker is registered in fieldworker questionnaire }
    if QB309 <> notappl & !loadcase( FIELDWKQ, QB309 ) then
      errmsg( 0026, QB309, tr("anthropometry"), tr("measurer"), tr("man"), i );
    endif;
    if QB310 <> notappl & !loadcase( FIELDWKQ, QB310 ) then
      errmsg( 0026, QB310, tr("anthropometry"), tr("assistant"), tr("man"), i );
    endif;
    if QB323 <> notappl & !loadcase( FIELDWKQ, QB323 ) then
      errmsg( 0026, QB323, tr("other test"), tr("measurer"), tr("man"), i );
    endif;
    if QB327 <> notappl & !loadcase( FIELDWKQ, QB327 ) then
      errmsg( 0026, QB327, tr("other test"), tr("measurer"), tr("man"), i );
    endif;
    if QB315 <> notappl & !loadcase( FIELDWKQ, QB315 ) then
      errmsg( 0026, QB315, tr("other test"), tr("measurer"), tr("man"), i );
    endif;

    { !!! HIV - other test }
    if length( strip(QB340) ) & !QB340 in "99992":"99996","?" then
      do k = 1 while k <= tbcodesHIV
        if QB340 = barcodeHIV(k) then
          if transHIV(k) then
            errmsg( 0173, QHCLUST, QHNUMBER, barcodeHIV(k), tr("man"), tr("HIV"), i );
          endif;
          transHIV(k) = transHIV(k) + 1;
          break;
        endif;
      enddo;
      if k > tbcodesHIV then
        errmsg( 0174, QHCLUST, QHNUMBER, QB340, tr("man"), tr("HIV"), i );
      endif;
    endif;
    { !!! HIV }
  enddo;

