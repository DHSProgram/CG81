{Application 'GEN_S' logic file generated by CSPro}
PROC GLOBAL
numeric c,i;

numeric maxhh = 15;
numeric maxcluster = 130;
numeric preassign = 0;
numeric firsttime = 0;
numeric allmalesel = 0; // set to 1 if generating clusters for training

PROC CNULL_FF
clear(SAMPSEL);
if firsttime then
  setfile(SAMPSEL, ".\PII\REF\SAMPSEL.csdb", create);
else
  setfile(SAMPSEL, ".\PII\REF\SAMPSEL.csdb", update);
endif;
forcase CLUSTERS do

  if !YCLUSTER in 9971:9975, 9981:9985 then
    next;
  endif;
  if preassign then
    setfile(HHFORINT, ".\PII\WORK\S"+edit("9999",c)+".DAT", create);
  endif;
  clear(HHFORINT);
  clear(SAMPSEL);
  XCLUSTER = YCLUSTER;
  XTOTAL = maxhh;
  XCLDATE = sysdate("YYYYMMDD");
  XCHANGE = 2;
  do i = 1 while i <= XTOTAL
     VNUMBER = i;
     VSTRUCT = i;
     VADDRESS = maketext("Address Ménage %03d", i);
     VNAME = maketext("Chef du ménage %03d", i);
     if i % 2 | allmalesel then
       VMALE = 1;
     else
       VMALE = 2;
     endif;
     if preassign then
       recode c  -> VINTCODE;
         1-10    -> 100+int(i/10)+1;
         11-20   -> 200+int(i/10)+1;
       endrecode;
     else
       VINTCODE = 0;
     endif;
     VRESULT = 0;
     XNUMBER(i) = VNUMBER;
     XSTRUCT(i) = VNUMBER;
     XADDRESS(i) = VADDRESS;
     XNAME(i) = VNAME;
     XMALE(i) = VMALE;
     XANEMIA(i) = 1;
     XINTCODE(i) = 0;
     XRESULT(i) = VRESULT;
     if preassign then
       writecase(HHFORINT);
     endif;
  enddo;
  writecase(SAMPSEL);
enddo;
// TOT
