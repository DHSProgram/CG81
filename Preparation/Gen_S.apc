{Application 'GEN_S' logic file generated by CSPro}
PROC GLOBAL
numeric c,i;

numeric maxhh = 9;
numeric maxcluster = 130;
numeric preassign = 0;
numeric firsttime = 0;

PROC CNULL_FF
clear(SAMPSEL);
if firsttime then
  setfile(SAMPSEL, ".\SAMPSEL.csdb", create);
else
  setfile(SAMPSEL, ".\SAMPSEL.csdb", update);
endif;
forcase CLUSTERS do

  if YCLUSTER < 9910 then
    next;
  endif;
  if preassign then
    setfile(HHFORINT, ".\PII\WORK\S"+edit("9999",c)+".DAT", create);
  endif;
  clear(HHFORINT);
  clear(SAMPSEL);
  XCLUSTER = YCLUSTER;
  XTOTAL = maxhh;
  XCLDATE = sysdate("YYYYMMDD");
  XCHANGE = 2;
  do i = 1 while i <= XTOTAL
     VNUMBER = i;
     VSTRUCT = i;
     VADDRESS = maketext("Address Ménage %03d", i);
     VNAME = maketext("Chef du ménage %03d", i);
     if XCLUSTER > 9000 then // !!! training clusters, all hh's male sub-sample
       VMALE = 1;
     else             // clusters for field practice - 1/2 male and 1/3 male dv in long/male hh's
       if i % 2 then
         VMALE = 1;
       else
         VMALE = 2;
       endif;
     endif;
     if preassign then
       recode c  -> VINTCODE;
         1-10    -> 100+int(i/10)+1;
         11-20   -> 200+int(i/10)+1;
       endrecode;
     else
       VINTCODE = 0;
     endif;
     VRESULT = 0;
     XNUMBER(i) = VNUMBER;
     XSTRUCT(i) = VNUMBER;
     XADDRESS(i) = VADDRESS;
     XNAME(i) = VNAME;
     XMALE(i) = VMALE;
     XANEMIA(i) = 1;
     XINTCODE(i) = 0;
     XRESULT(i) = VRESULT;
     if preassign then
       writecase(HHFORINT);
     endif;
  enddo;
  writecase(SAMPSEL);
enddo;
// TOT
