{Application 'CAPTUREGPS' logic file generated by CSPro }
PROC GLOBAL

  numeric debug = 1;
  numeric xcluster, xintnum, i, captured, capturenext = 0, justquit = 0, x;
  string gpstitle;

  { store original values from LINTRO variable }
  array origval(500);    

  { setup basic user bar }
  function userbase();
    userbar( clear );
    userbar( add button, "<",    do("PreviousField") );
    userbar( add button, ">",    do("NextField") );
    userbar( add button, ">>|",  do("AdvanceToEnd") );
    userbar( add button, "Lang", do("ChangeLanguage") );
	
  end;

  { set value sets based on language }
  function OnChangeLanguage()
    setLanguage( getlanguage() );
  end;

PROC FL_CNULL
preproc
  if debug then
    // errmsg(  "WARNING: DEBUG MODE ENABLED!" );
    trace(on, ".\debug.app", clear);
//  trace(on);
    set trace;
  endif;
  { get language from calling menu }
  SetLanguage( GetLanguage() );

  xcluster = tonumber( sysparm()[1:4] );
  xintnum  = tonumber( sysparm()[5:4] );

  setfont( ValueSets, "Arial", 24, bold );

  enter GPS_FF;
  stop(1);

PROC GPSSEC_FORM
preproc

  userbase();
  userbar( show );

  GCLUSTER  = xcluster;
  captured  = 0;
  { open the GPS dongle to capture GPS coordinates }
  { !!! verify using the device manager the port number used by the dongle       }
  {     in ICF laptops port 3 is reserved and erroneously taken as a dongle port }
  {     in ASUS tablets ports are assigned starting from port 3 and above        }
  if getOS() = 20 then // Android
    i = GPS(open);
  else                // Windows
    do i = 3 while i <= 10
      if GPS( open, i, 4800 ) then
        break
      endif;
    enddo;
  endif;
  if !i | i > 10 then
    errmsg( 60047 );
    endsect;
  else
    {chek if coordinates previously taken and delete record if that is the case }
    loadcase( GPS_DCT, GCLUSTER );
  endif;

postproc
  if captured then
    i = totocc(GPSSEC);
    errmsg(60058, GLATITUDE(i), GLATPOLE(i), GLONGITUDE(i),GLNGPOLE(i) );
    writecase( GPS_DCT );
  endif;
  gps( close );

PROC GPSSEC000

PROC GCOLUMN
preproc
  $ = curocc();

PROC GINTRO
preproc
  trace("capturenext=%d, GCOLUMN=%d, glat=%f", capturenext, GCOLUMN,visualvalue( GLATITUDE(GCOLUMN) ) );
  if capturenext & GCOLUMN <= 4 & visualvalue( GLATITUDE(GCOLUMN) ) <> notappl then
    $ = 2;
    noinput;
  elseif justquit then
    $ = 3;
    noinput;
  else
    capturenext = 0;
    $ = notappl;
  endif;

postproc
  if $ = 1 & visualvalue( GLATITUDE(GCOLUMN) ) <> notappl then
    x = accept( tr("By choosing this option new coordinates will replace the current readings, Do you want to replace them?"), tr("Yes"), tr("No") );
	if x = 2 then
      reenter
    endif;
  elseif $ = 3 & visualvalue( GLATITUDE(GCOLUMN) ) <> notappl then
    justquit = 1;
    capturenext = 1;
    advance; advance;
  elseif $ = 3 then
    endsect
  elseif $ = 2 then
    if GCOLUMN <= 4 & visualvalue( GLATITUDE(GCOLUMN) ) <> notappl then
      capturenext = 1;
      advance; advance;
    else
      errmsg( 60055 );
      reenter;
    endif;
  endif;

PROC GLATITUDE
preproc
  if !capturenext then
    gps(close);
    gps(open);
    gpstitle = tr("GPS device acquiring GPS satellites..., Please wait for 10 seconds to complete the capture");
    if gps(read, 55, 50, gpstitle ) then
      GLATITUDE  = gps( latitude );
      GLATPOLE   = "N";
      if gps( latitude ) < 0 then	{ negative = south }
        GLATPOLE = "S";
      endif;
      GLONGITUDE = gps( longitude );
      GLNGPOLE  = "E";
      if gps( longitude ) < 0 then	{ negative = west }
        GLNGPOLE = "W";
      endif;
     	GALTITUDE  = gps( altitude );
      GSATELLIT = gps( satellites );	  { no. of satellites, at least 3-4 is recommended }
      GACCURACY = gps( accuracy );	    { precision level, 1 is the best possible and 50 the least accurate }
      captured  = 1;
    else
      errmsg( 60052 );
      reenter GINTRO;
    endif;
  endif;
postproc
  // errmsg("lat=%f, long=%f, sat=%d, accuracy=%d", GLATITUDE, GLONGITUDE, GSATELLIT, GACCURACY);
